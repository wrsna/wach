# Brane Å½dralo (19.1.2021)

# _iface is "olimex" or "ftdi"
set _iface "olimex"
#set _iface "ftdi"

# _upload is "old" or "new"
set _upload "old"
#set _upload "new"

if {$_iface=="olimex"} {
  source [find interface/ftdi/olimex-arm-usb-ocd-h.cfg]
  transport select jtag
}

if {$_iface=="ftdi"} {
  source [find interface/ftdi/minimodule-swd.cfg]
}

source [find target/at91sam3ax_8x.cfg]

init
halt

$_TARGETNAME configure -event gdb-attach {
    setup_gpnvm
    if {$_upload=="old"} {
        upload_flash $_flash_file
    } else {
        program $_flash_file.elf preverify verify
    }
    reset_hard $_iface
    reset halt
    mww 0x400e1a00 0xa500000c
    #adapter speed 150
}

$_TARGETNAME configure -event gdb-detach {
    shutdown
}

proc setup_gpnvm {} {
    echo "**Checking gpnvm flag.**"
    set _response [capture "at91sam3 gpnvm show"]
    regexp {(sam3-gpnvm1:).([01])} $_response _dummy _dummy _flg
    echo "**gpnvm1 currently set to $_flg.**"
    if {$_flg==0} {
        echo "**Setting gpnvm1.**"
        at91sam3 gpnvm set 1
    }
}

proc upload_flash {_file} {
     echo "Comparing flash to $_file.bin"

    if { [catch {set _response [capture "flash verify_bank 0 $_file.bin 0"]]} res] } {
        set _response "contents differ error"
    }
    regexp {(contents).([A-Za-z0-9]*)} $_response _dummy _dummy _match

    if {$_match=="match"} {
        echo "File matches flash. No need to upload."
    } else {
        echo "File differs from flash. Uploading $_file.elf."
        set _response [capture "program $_file.elf verify"]
        regexp {(Verified).([A-Za-z0-9]*)} $_response _dummy _dummy _match
        if {$_match=="OK"} {
            echo "Upload OK."
        } else {
            echo "Upload FAILED."
        }
    }
}

proc reset_hard {_iface} {
    if {$_iface=="ftdi"} {
        echo "External RESET"
        ftdi_set_signal nSRST 0
        after 10
        ftdi_set_signal nSRST 1
        after 200
    }
}
